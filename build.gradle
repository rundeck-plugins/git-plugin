plugins {
    alias(libs.plugins.axionRelease)
    alias(libs.plugins.nexusPublish)
    id 'java'
}

group 'com.rundeck'

ext.rundeckPluginVersion = '1.2'
ext.publishName = "Git Plugin ${project.version}"
ext.githubSlug = 'rundeck-plugins/git-plugin'
ext.pluginClassNames='com.rundeck.plugin.GitResourceModelFactory,com.rundeck.plugin.GitCloneWorkflowStep,com.rundeck.plugin.GitPushWorkflowStep,com.rundeck.plugin.GitCommitWorkflowStep,com.rundeck.plugin.GitAddWorkflowStep'
ext.pluginName = 'Git Plugin'
ext.pluginDescription = 'This is a git plugin (based on Jgit) which contain a Resource model and worflow steps'
ext.developers = [
        [id: 'ltamaster', name: 'Luis Toledo', email: 'luis@variacode.com']
]


java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = ''
        versionSeparator = ''
    }
}
project.version = scmVersion.version

apply plugin: 'groovy'
apply plugin: 'java'

repositories {
    mavenCentral()
}

configurations {
    pluginLibs

    implementation {
        extendsFrom pluginLibs
    }
}


dependencies {
    // Use catalog aliases for dependencies
    implementation(libs.groovyAll)
    implementation(libs.rundeckCore)
    implementation(libs.slf4jApi)

    pluginLibs(libs.jgit) {
        exclude module: 'slf4j-api'
        exclude module: 'jsch'
        exclude module: 'commons-logging'
    }

    pluginLibs(libs.jgitSsh) {
        exclude module: 'slf4j-api'
        exclude group: 'org.bouncycastle'
    }

    testImplementation libs.bundles.testLibs
}


task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')
        attributes 'Rundeck-Plugin-Name' : pluginName
        attributes 'Rundeck-Plugin-Description' : pluginDescription
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '2.11.4+'
        attributes 'Rundeck-Plugin-Tags': 'java,workflow steps,resource model'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/git-plugin'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': project.version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
    dependsOn(copyToLib)
}

nexusPublishing {
    packageGroup = 'org.rundeck.plugins'
    repositories {
        sonatype()
    }
}

test {
    useJUnitPlatform()
}

apply from: "${rootDir}/gradle/publishing.gradle"
