plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.15.4'
}

group 'com.rundeck'

ext.rundeckPluginVersion = '1.2'
ext.pluginClassNames='com.rundeck.plugin.GitResourceModelFactory,com.rundeck.plugin.GitCloneWorkflowStep,com.rundeck.plugin.GitPushWorkflowStep,com.rundeck.plugin.GitCommitWorkflowStep,com.rundeck.plugin.GitAddWorkflowStep'
ext.pluginName = 'Git Plugin'
ext.pluginDescription = 'This is a git plugin (based on Jgit) which contain a Resource model and worflow steps'

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = ''
        versionSeparator = ''
    }
}
project.version = scmVersion.version

apply plugin: 'groovy'
apply plugin: 'java'

repositories {
    mavenCentral()
}

configurations {
    pluginLibs

    implementation {
        extendsFrom pluginLibs
    }
}


dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.9'
    testImplementation group: 'junit', name: 'junit', version: '4.12'

    implementation group: 'org.rundeck', name: 'rundeck-core', version: '4.15.0-20230725'
    implementation 'org.slf4j:slf4j-api:1.7.30'

    pluginLibs( 'org.eclipse.jgit:org.eclipse.jgit:5.13.2.202306221912-r') {
        exclude module: 'slf4j-api'
        exclude module: 'jsch'
        exclude module: 'commons-logging'
    }

    pluginLibs ("org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:5.13.2.202306221912-r"){
        exclude module: 'slf4j-api'
        exclude group: 'org.bouncycastle'
    }

    testImplementation "org.codehaus.groovy:groovy-all:3.0.9"
    testImplementation "org.spockframework:spock-core:2.0-groovy-3.0"
    testImplementation "cglib:cglib-nodep:2.2.2"
    testImplementation 'org.objenesis:objenesis:1.4'
}


task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')
        attributes 'Rundeck-Plugin-Name' : pluginName
        attributes 'Rundeck-Plugin-Description' : pluginDescription
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '2.11.4+'
        attributes 'Rundeck-Plugin-Tags': 'java,workflow steps,resource model'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/git-plugin'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': project.version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
    dependsOn(copyToLib)
}
